/*
 * Copyright (c) 2021 Henry 李恒 (henry.box@outlook.com).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.gradle.plugin-publish'  version '0.14.0' apply false
    id 'com.palantir.git-version'   version '0.12.3'
}

subprojects {
    version 'unspecified' != version ? version : gitVersion()

    apply {
        plugin 'groovy'
        plugin 'maven-publish'
        plugin 'java-gradle-plugin'
        plugin 'com.gradle.plugin-publish'
    }

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://repo.spring.io/release' }
        mavenCentral()
    }

    dependencies {
        implementation gradleApi()
    }

    jar {
        manifest {
            attributes(
                'Implementation-Title'      : project.name,
                'Automatic-Module-Name'     : project.name.replaceAll('-', '.'),
                'Implementation-Version'    : project.version,
                'Implementation-Vendor'     : 'IHub',
                'Created-By'                : 'Java ' + JavaVersion.current().majorVersion
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                from components.java
            }
        }
    }

    pluginBundle {
        website     = 'https://github.com/ihub-pub/plugins'
        vcsUrl      = 'https://github.com/ihub-pub/plugins.git'
        description = 'IHub Plugins Gradle Plugin'
        tags        = ['ihub', 'groovy', 'gradle', 'spring', 'boot', 'java', 'native']
    }
}
